program -> declaration* EOF;

declaration -> varDeclaration | statement;
varDeclaration -> "var" IDENTIFIER ("=" expression)? ";";

statement -> exprStmt | ifStmt | whileStmt | forStmt | breakStmt | continueStmt | returnStmt | block;
exprStmt -> expression ";";
breakStmt -> "break" ";";
continueStmt -> "continue" ";";
returnStmt -> "return" expression? ";";

block -> "{" declaration* "}";
ifStmt -> "if" "(" expression ")" statement ("elif" "(" expression ")" statement)* ("else" statement)?;
whileStmt -> "while" "(" expression ")" statement;
forStmt -> "for" "(" forInit? ";" expression? ";" expression? ")" statement;
forInit -> varDeclaration | exprStmt;

expression -> assignment;
assignment -> logicalOr (assignmentOp assignment)?;
assignmentOp -> "=" | "+=" | "-=";
logicalOr -> logicalAnd ("or" logicalAnd)*;
logicalAnd -> bitwiseOr ("and" bitwiseOr)*;

bitwiseOr -> bitwiseXor ("|" bitwiseXor)*;
bitwiseXor -> bitwiseAnd ("^" bitwiseAnd)*;
bitwiseAnd -> equality ("&" equality)*;

equality -> comparison (("==" | "!=") comparison)*;
comparison -> shift (("<" | "<=" | ">=" | ">") shift)*;

shift -> addition (("<<" | ">>") addition)*;
addition -> multiplication (("+" | "-") multiplication)*;
multiplication -> power (("*" | "/" | "%") power)*;
power -> unary ("**" unary)*;
unary -> ("!" | "-" | "~") unary | postfix;

postfix -> primary (postfixOp)*;
postfixOp -> "(" arguments? ")" | "[" expression "]" | "." IDENTIFIER;
arguments -> expression ("," expression)*;

primary -> NUMBER | STRING_LITERAL | "true" | "false" | IDENTIFIER | "(" expression ")";
